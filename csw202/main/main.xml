<?xml version="1.0" encoding="UTF-8"?>
<ctl:package
 xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
 xmlns:ctl="http://www.occamlab.com/ctl"
 xmlns:parsers="http://www.occamlab.com/te/parsers"
 xmlns:p="http://teamengine.sourceforge.net/parsers"
 xmlns:saxon="http://saxon.sf.net/"
 xmlns:csw="http://www.opengis.net/cat/csw/2.0.2"
 xmlns:ows="http://www.opengis.net/ows"
 xmlns:dc="http://purl.org/dc/elements/1.1/" 
 xmlns:dct="http://purl.org/dc/terms/" 
 xmlns:xlink="http://www.w3.org/1999/xlink" 
 xmlns:xi="http://www.w3.org/2001/XInclude"
 xmlns:xsd="http://www.w3.org/2001/XMLSchema">

  <xi:include href="common.xml"/>
  <xi:include href="capability-tests.xml"/>

  <ctl:suite name="csw:csw-2.0.2-compliance-suite">
    <ctl:title>CSW 2.0.2 Compliance Test Suite</ctl:title>
    <ctl:description>
       Verifies a CSW 2.0.2 catalogue implementation against all applicable 
       abstract test suites. No specific CSW application profile is presumed.
       These tests are intended to be incorporated into profile-specific test 
       suites.
    </ctl:description>
    <ctl:link>data/data-csw-2.0.2.zip</ctl:link>
    <ctl:link>docs/csw/2.0.2/</ctl:link>
    <ctl:starting-test>csw:Main</ctl:starting-test>
  </ctl:suite>

  <ctl:test name="csw:Main">
    <ctl:assertion>The implementation under test minimally conforms to the CSW 
      part (Clause 10) of Catalogue 2.0.2 (OGC 07-006r1).</ctl:assertion>
    <ctl:code>         
      <xsl:variable name="form-values">
        <ctl:form height="600" width="800">
          <div id="input-form" xmlns="http://www.w3.org/1999/xhtml">
            <h2>CSW Catalogue 2.0.2 - Test setup</h2>
            <h3>Service metadata</h3>
            <p>
                  Please provide a URL from which a capabilities document for 
                  the CSW implementation under test can be retrieved (this can 
                  refer to a static document or to a service endpoint).
            </p>
            <br />
            <table border="2" padding="4" bgcolor="#00ffff">
              <tr>
                <td align="left">Capabilities URL</td>
                <td align="center">
                  <input name="capabilities.url" size="128" 
                                 type="text" value="http://"/>
                </td>
              </tr>
            </table>
            <p>
              <div bgcolor="#ffffcc"><strong>WARNING: </strong> Don't forget to add the test data!</div>
            </p>
            <br />
            <input type="submit" value="Start"/>
          </div>
        </ctl:form>
      </xsl:variable>

      <!-- Populate global variables from form data -->
      <xsl:variable name="csw.capabilities.url" 
                      select="$form-values/values/value[@key='capabilities.url']"/>

      <!-- Attempt to retrieve capabilities document -->
      <xsl:variable name="csw.capabilities.doc">
        <ctl:request>
          <ctl:url>
            <xsl:value-of select="$csw.capabilities.url"/>
          </ctl:url>
          <ctl:method>GET</ctl:method>
        </ctl:request>
      </xsl:variable>

      <xsl:choose>
        <xsl:when test="not($csw.capabilities.doc/csw:Capabilities)">
          <ctl:message>[FAILURE] Did not obtain a csw:Capabilities document! Skipping remaining tests.</ctl:message>	
          <ctl:fail/>
        </xsl:when>
        <xsl:otherwise>
          <xsl:variable name="SmokeTest.result">
            <ctl:call-test name="csw:SmokeTest">
              <ctl:with-param name="csw.capabilities.doc" select="$csw.capabilities.doc"/>
            </ctl:call-test>
          </xsl:variable>
          <xsl:if test="not($SmokeTest.result/*)">
            <ctl:call-test name="csw:capability-tests">
              <ctl:with-param name="csw.capabilities.doc" 
                                 select="$csw.capabilities.doc"/>															
            </ctl:call-test>
          </xsl:if>
        </xsl:otherwise>
      </xsl:choose>
    </ctl:code>
  </ctl:test>

  <ctl:test name="csw:SmokeTest">
    <ctl:param name="csw.capabilities.doc"/>
    <ctl:assertion>Confirm that the IUT is ready to undergo testing.</ctl:assertion>
    <ctl:comment>
        Check all of the following conditions:
        (1) the service is available;
        (2) the capabilities document is valid;
        (3) test data have been loaded.
        If any of these checks fail, the test run is aborted.
    </ctl:comment>
    <ctl:code>
      <ctl:call-test name="ctl:SchematronValidatingParser">
        <ctl:with-param name="doc" select="$csw.capabilities.doc" />
        <ctl:with-param name="schema">/sch/csw/2.0.2/Capabilities.sch</ctl:with-param>
        <ctl:with-param name="phase">RequiredElementsPhase</ctl:with-param>
      </ctl:call-test>

      <xsl:variable name="GetCapabilities.get.endpoint">
        <xsl:value-of select="$csw.capabilities.doc//ows:OperationsMetadata/ows:Operation[@name='GetCapabilities']/ows:DCP/ows:HTTP/ows:Get/@xlink:href"/>
      </xsl:variable>
      <xsl:variable name="GetRecordById.get.endpoint">
        <xsl:value-of select="$csw.capabilities.doc//ows:OperationsMetadata/ows:Operation[@name='GetRecordById']/ows:DCP/ows:HTTP/ows:Get/@xlink:href"/>
      </xsl:variable>
      <xsl:choose>
        <xsl:when test="not(starts-with($GetCapabilities.get.endpoint,'http'))">
          <ctl:message>
                  [FAILURE] Required endpoint for GetCapabilities using GET method not found in capabilities document.
          </ctl:message>
          <ctl:fail />
        </xsl:when>
        <xsl:when test="not(starts-with($GetRecordById.get.endpoint,'http'))">
          <ctl:message>
                  [FAILURE] Required endpoint for GetRecordById using GET method not found in capabilities document.
          </ctl:message>
          <ctl:fail />
        </xsl:when>
        <xsl:otherwise>
          <xsl:variable name="GetCapabilitiesRsp"> 
            <ctl:request>
              <ctl:url>
                <xsl:value-of select="$GetCapabilities.get.endpoint"/>
              </ctl:url>
              <ctl:method>GET</ctl:method>
              <ctl:param name="service">CSW</ctl:param>
              <ctl:param name="version">2.0.2</ctl:param>
              <ctl:param name="request">GetCapabilities</ctl:param>
              <p:XMLValidatingParser.CSW/>
            </ctl:request>
          </xsl:variable>

          <xsl:variable name="GetRecordByIdRsp">
            <ctl:request>
              <ctl:url>
                <xsl:value-of select="$GetRecordById.get.endpoint"/>
              </ctl:url>
              <ctl:method>GET</ctl:method>
              <ctl:param name="service">CSW</ctl:param>
              <ctl:param name="version">2.0.2</ctl:param>
              <ctl:param name="request">GetRecordById</ctl:param>
              <ctl:param name="id">urn:uuid:94bc9c83-97f6-4b40-9eb8-a8e8787a5c63</ctl:param>
              <p:XMLValidatingParser.CSW/>
            </ctl:request>
          </xsl:variable>

          <xsl:if test="not($GetCapabilitiesRsp/csw:Capabilities)">
            <ctl:message>
                  [FAILURE] The response entity is NOT a csw:Capabilities document.
                  The document element has [local name] = <xsl:value-of select="local-name($GetCapabilitiesRsp/*[1])"/> and [namespace name] = <xsl:value-of select="namespace-uri($GetCapabilitiesRsp/*[1])"/>.
            </ctl:message>
            <ctl:fail/>
          </xsl:if>

          <xsl:if test="count($GetRecordByIdRsp/csw:GetRecordByIdResponse/*) != 1">
            <ctl:message>
                  [FAILURE] The GetRecordByIdResponse entity does NOT include only one matching item from the test data set (id = urn:uuid:94bc9c83-97f6-4b40-9eb8-a8e8787a5c63).
            </ctl:message>
            <ctl:fail/>
          </xsl:if>
        </xsl:otherwise>
      </xsl:choose>
    </ctl:code>
  </ctl:test>
</ctl:package>
